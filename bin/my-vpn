#!/usr/bin/env osascript

global tokenValue
on get_token(t)
  tell application "Microsoft Edge"
    tell t to execute javascript "document.getElementById('token')?.value || ''"
  end tell
end get_token

on is_on_auth_form(t)
	tell application "Microsoft Edge"
		tell t to execute javascript "document.forms[0].lastElementChild.innerText.includes('AUTHORIZE ME')"
	end tell
end is_on_auth_form

on is_really_on_auth_form(t)
	if is_on_auth_form(t) is true then
		delay 1
		return is_on_auth_form(t)
	end if
	return false
end is_really_on_auth_form

on submit_form(t)
  delay 0.5
  tell application "Microsoft Edge"
    tell t to execute javascript "document.forms[0].submit()"
  end tell
end submit_form

on run argv
  tell application "Microsoft Edge"
    if it is not running then
      activate
      delay 5
    end if
    tell first window
      set tokenWindow to make new tab with properties {URL:"https://fido-challenger.githubapp.com/auth/vpn-prod"}
      repeat while my is_really_on_auth_form(tokenWindow) is false
        delay 0.5
      end repeat

      my submit_form(tokenWindow)
      repeat while my get_token(tokenWindow) is ""
        delay 0.5
      end repeat
      set tokenValue to my get_token(tokenWindow)
      tell tokenWindow to close
    end tell
  end tell

  tell application "Viscosity" to connect "github-iad-prod"

  tell application "System Events"
    tell process "Viscosity"
      delay 2
      repeat until exists text field 1 of window 1
      end repeat
      set value of text field 1 of window 1 to tokenValue
      click button "OK" of window 1
    end tell
  end tell
end run